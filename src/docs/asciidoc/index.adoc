= Ersatz Server User Guide
Christopher J. Stehno <chris@stehno.com>
v3.0.0, January 2021
:toc: left
:toclevels: 4



== Configuration

=== Content Translation


==== Encoders

The Encoders are used to convert response configuration data types into the outbound request content string. They are implemented as a
`Function<Object,byte[]>` with the input `Object` being the configuration object being converted, and the `byte[]` is the return type.

The various configuration levels have the same method signature:

[source,groovy]
----
ServerConfig encoder(String contentType, Class objectType, Function<Object, byte[]> encoder)
----

The `contentType` is the response content type to be encoded and the `objectType` is the type of configuration object to be encoded - this allows for
the same content-type to have different encoders for different configuration object types.

A simple example of an encoder would be the default JSON encoder (provided in the `com.stehno.ersatz.encdec.Encoders` class):

[source,groovy]
----
static final Function<Object, byte[]> json = { obj -> (obj != null ? toJson(obj) : '{}').bytes }
----

You may also configure encoders as a Groovy `Closure` with the same parameters:

[source,groovy]
----
def server = new ErsatzServer({
    encoder('application/json',Map){ obj->
        (bj != null ? toJson(obj) : '{}').bytes
    }
})
----

The two approaches are functionally equivalent.

===== JSON Encoders

As of 3.0.0 the built-in JSON Encoder was removed (to avoid dependency lock-in). Below are provided some simple/common implementations of them for use in your code:

*Groovy.* An encoder (serializer) implemented with the Groovy `JsonObject` could be implemented as:

public static final Function<Object, byte[]> json = obj -> (obj != null ? toJson(obj) : "{}").getBytes(UTF_8);

[source,groovy]
----
encoder(ContentType.APPLICATION_JSON, MyType){ obj ->
    JsonObject.toJson(obj).getBytes(UTF_8)
}
----

*Jackson.* A decoder (parser) implemented using the Jackson JSON library could be implemented as:

[source,java]
----
encoder(ContentType.APPLICATION_JSON, MyType.class, obj -> {
    return new ObjectMapper().writeValueAsBytes(obj);
});
----

=== Expectations

Request expectations are the core of the Ersatz server functionality; conceptually, they are HTTP server request routes which are used to match an
incoming HTTP request with a request handler or to respond with a status of 404, if no matching request was configured. The expectations are
configured on an instance of the `Expectations` interface, which provides multiple configuration methods for each of the supported HTTP request
methods (GET, HEAD, POST, PUT, DELETE, PATCH, OPTIONS, and TRACE), with the method name corresponding to the HTTP request method name. The four
general types of methods are:

* One taking a `String path` returning an instance of the `Request` interface
* One taking a `String path` and a `Consumer<Request>` returning an instance of the `Request` interface
* One taking a `String path` and a Groovy `Closure` returning an instance of the `Request` interface
* All of the above with the `String path` replaced by a Hamcrest `Matcher<String>` for matching the path

The `Consumer<Request>` methods will provide a `Consumer<Request>` implementation to perform the configuration on a `Request` instance passed into
the consumer function. The `path` strings in the verb methods may be called with `*` as a wildcard value - this will match any request with that
request method (e.g. `GET('*')` would match any GET request while `any('*')` could be used to match _ANY_ request made on the server).

The `Closure` support is similar to that of the consumer; however, this is a Groovy DSL approach where the `Closure` operations are delegated onto the
a `Request` instance in order to configure the request.

All of the expectation method types return an instance of the request being configured (`Request` or `RequestWithContent`).

There is also an `ANY` request method matcher configuration which will match a request regardless of the request method, if it matches the rest of the
configured criteria.

The primary role of expectations is to provide a means of matching incoming requests in order to respond in a desired and repeatable manner. They are
used to build up matching rules based on request properties to help filter and route the incoming request properly. http://hamcrest.org/[Hamcrest]
Matcher support allows for flexible request matching based on various request properties.

The configuration interfaces support three main approaches to configuration, a chained builder approach, such as:

[source,groovy]
----
HEAD('/foo')
    .query('a','42')
    .cookie('stamp','1234')
    .respond().header('ok','true')
----

where the code is a chain of builder-style method calls used to wire up the request expectation. The second method is available to users of the Groovy
language, the Groovy DSL approach would code the same thing as:

[source,groovy]
----
HEAD('/foo'){
    query 'a', '42'
    cookie 'stamp', '1234'
    responder {
        header 'ok', "true"
    }
}
----

which can be more expressive, especially when creating more complicated expectations. A third approach is a Java-based approach more similar to the
Groovy DSL, using the `Consumer<?>` methods of the interface, this would yield:

[source,java]
----
HEAD('/foo', req -> {
    req.query("a", "42")
    req.cookie("stamp", "1234")
    req.responder( res-> {
        res.header("ok", "true")
    })
})
----

Any of the three may be used in conjunction with each other to build up expectations in the desired manner.

TIP: The matching of expectations is perform in the order the expectations are configured, such that if an incoming request could be matched by more
than one expectation, the first one configured will be applied.

Request expectations may be configured to respond differently based on how many times a request is matched, for example, if you wanted the first
request of `GET /something` to respond with `Hello` and second (and all subsequent) request of the same URL to respond with `Goodbye`, you would
configure multiple responses, in order:

[source,groovy]
----
GET('/something'){
    responder {
        content 'Hello'
    }
    responder {
        content 'Goodbye'
    }
    called 2
}
----

Adding the `called` configuration adds the extra safety of ensuring that if the request is called more than our expected two times, the verification
will fail (and with that, the test).

Expectations may be cleared from the server using the `clearExpectations()` method. This is useful when you need to redefine expectations for one
test only, but all of the others have a common set of expectations.

==== Request Methods

The Ersatz server supports all of the standard HTTP request headers along with a few non-standard ones. The table below denotes the supported methods
their contents.

|====
|Method  |Request Body |Response Body |Reference
|GET     | N           | Y            | https://www.w3.org/Protocols/rfc2616/rfc2616-sec9.html#sec9.3[RFC2616 Sec 9.3]
|HEAD    | N           | N            | https://www.w3.org/Protocols/rfc2616/rfc2616-sec9.html#sec9.4[RFC2616 Sec 9.4]
|OPTIONS | N           | N            | https://www.w3.org/Protocols/rfc2616/rfc2616-sec9.html#sec9.2[RFC2616 Sec 9.2]
|POST    | Y           | Y            | https://www.w3.org/Protocols/rfc2616/rfc2616-sec9.html#sec9.5[RFC2616 Sec 9.5]
|PUT     | Y           | N            | https://www.w3.org/Protocols/rfc2616/rfc2616-sec9.html#sec9.6[RFC2616 Sec 9.6]
|DELETE  | N           | N            | https://www.w3.org/Protocols/rfc2616/rfc2616-sec9.html#sec9.7[RFC2616 Sec 9.7]
|PATCH   | Y           | N            | https://tools.ietf.org/html/rfc5789[RFC5789]
|TRACE   | N           | Y            | https://www.w3.org/Protocols/rfc2616/rfc2616-sec9.html#sec9.8[RFC2616 Sec 9.8]
|====

The following sections describe how each method is supported with a simple example.

While Ersatz does constrain the content of the request and response based on the request method, it is generally up to the mocker to provide the
desired and/or appropriate responses (including most headers). This implementation leniency is intentional, and is meant to allow for endpoint
implementations that do not necessarily follow the published specification, but likewise still need to be tested as they really exist rather than how
they _should_ exist.

===== HEAD

A `HEAD` request is used to retrieve the headers for a URL, basically a `GET` request without any response body. An Ersatz mocking example would be:

[source,groovy]
----
ersatzServer.expectations {
    HEAD('/something').responds().header('X-Alpha','Interesting-data').code(200)
}
----

which would respond to `HEAD /something` with an empty response and the response header `X-Alpha` with the specified value.

===== GET

The `GET` request is a common HTTP request, and what browsers do by default. It has no request body, but it does have response content. You mock `GET` requests
using the `get()` methods, as follows:

[source,groovy]
----
ersatzServer.expectations {
    GET('/something').responds().body('This is INTERESTING!', 'text/plain').code(200)
}
----

In a RESTful interface, a `GET` request is usually used to "read" or retrieve a resource representation.

===== OPTIONS

The `OPTIONS` HTTP request method is similar to an `HEAD` request, having no request or response body. The primary response value in an `OPTIONS` request
is the content of the `Allow` response header, which will contain a comma-separated list of the request methods supported by the server. The request
may be made against a specific URL path, or against `*` in order to determine what methods are available to the entire server.

In order to mock out an `OPTIONS` request, you will want to respond with a provided `Allow` header. This may be done using the
`Response.allows(HttpMethod...)` method in the responder. An example would be something like:

[source,groovy]
----
ersatzServer.expectations {
    OPTIONS('/options').responds().allows(GET, POST).code(200)
    OPTIONS('/*').responds().allows(DELETE, GET, OPTIONS).code(200)
}
----

This will provide different allowed options for `/options` and for the "entire server" (`*`). You can also specify the `Allow` header as a standard response header.

Note that not all client and servers will support the `OPTIONS` request method.

===== POST

The `POST` request is often used to send browser form data to a backend server. It can have both request and response content.

[source,groovy]
----
ersatzServer.expectations {
    POST('/form'){
        body([first:'John', last:'Doe'], APPLICATION_URLENCODED)
        responder {
            body('{ status:"saved" }', APPLICATION_JSON)
        }
    }
}
----

In a RESTful interface, the `POST` method is generally used to "create" new resources.

===== PUT

A `PUT` request is similar to a `POST` except that while there is request content, there is no response body content.

[source,groovy]
----
ersatzServer.expectations {
    PUT('/form'){
        query('id','1234')
        body([middle:'Q'], APPLICATION_URLENCODED)
        responder {
            code(200)
        }
    }
}
----

In a RESTful interface, a `PUT` request if most often used as an "update" operation.

===== DELETE

A `DELETE` request has not request or response content. It would look something like:

[source,groovy]
----
ersatzServer.expectations {
    DELETE('/user').query('id','1234').responds().code(200)
}
----

In a RESTful interface, a `DELETE` request may be used as a "delete" operation.

===== PATCH

The `PATCH` request method creates a request that can have body content; however, the response will have no content.

[source,groovy]
----
ersatzServer.expectations {
    PATCH('/user'){
        query('id','1234')
        body('{ "middle":"Q"}', APPLICATION_JSON)
        responder {
            code(200)
        }
    }
}
----

In a RESTful interface, a `PATCH` request may be used as a "modify" operation for an existing resource.

===== TRACE

The `TRACE` method is generally meant for debugging and diagnostics. The request will have no request content; however, if the request is valid,
the response will contain the entire request message in the entity-body, with a Content-Type of `message/http`. With that in mind, the `TRACE`
method is implemented a bit differently than the other HTTP methods. It's not available for mocking, but it will provide an echo of the request as
it is supposed to. For example the following request (raw):

----
TRACE / HTTP/1.1
Host: www.something.com
----

would respond with something like the following response (raw):

----
HTTP/1.1 200 OK
Server: Microsoft-IIS/5.0
Date: Tue, 31 Oct 2006 08:01:48 GMT
Connection: close
Content-Type: message/http
Content-Length: 39

TRACE / HTTP/1.1
Host: www.something.com
----

Since this functionality is already designed for diagnostics purposes, it was decided that it would be best to simply implement and support the
request method rather than allow it to be mocked.

Making a `TRACE` request to Ersatz looks like the following:

[source,groovy]
----
ersatzServer.start()

URL url = new URL("${ersatzServer.httpUrl}/info?data=foo+bar")
HttpURLConnection connection = url.openConnection() as HttpURLConnection
connection.requestMethod = 'TRACE'

assert connection.contentType == MESSAGE_HTTP.value
assert connection.responseCode == 200

assert connection.inputStream.text.readLines()*.trim() == """TRACE /info?data=foo+barHTTP/1.1
    Accept: text/html, image/gif, image/jpeg, *; q=.2, */*; q=.2
    Connection: keep-alive
    User-Agent: Java/1.9.0.1_121
    Host: localhost:${ersatzServer.httpPort}
""".readLines()*.trim()
----

The explicit `start()` call is required since there are no expectations specified (auto-start wont fire). The `HttpUrlConnection` is used to make the
request, and it can be seen that the response content is the same as the original request content.

The `TRACE` method is supported using the built-in `HttpTraceHandler` provided by the embedded http://undertow.io[Undertow] server.

NOTE: At some point, if there are valid use cases for allowing mocks of `TRACE` it could be supported. Feel free to
https://github.com/cjstehno/ersatz/issues/new[create an Issue ticket] describing your use case and it will be addressed.

===== Verification

A timeout (and unit) parameter is available on the `verify` method so that a failed verification can fail-out in a timely manner, while still waiting for messages that are not coming.

TIP: The message communication is asynchronous, therefore messages captured by the client should consider that they may arrive out of order or after verification has occurred.

=== Request Matching

When a request comes into the server an attempt is made to match it against the configured request expectations. When a match is found, the configured
response it returned to the client; however, when no expectation matches the request a 404 response will be returned and a mismatch report will be
written to the logs, an example is shown below:

```text
# Unmatched Request

HTTP GET /alpha/foo ? selected=[one, two], id=[1002]
Headers:
    - alpha: [bravo-1, bravo-2]
    - charlie: [delta]
    - Content-Type: [text/plain]
Cookies:
    - ident (null, null): asdfasdfasdf
Character-Encoding: UTF-8
Content-type: text/plain
Content-Length: 1234
Content:
    [84, 104, 105, 115, 32, 105, 115, 32, 115, 111, 109, 101, 32, 116, 101, 120, 116, 32, 99, 111, 110, 116, 101, 110, 116]

# Expectations

Expectation 0 (2 matchers):
    X HTTP method matches <POST>
    ✓ Path matches "/alpha/foo"
    (2 matchers: 1 matched, 1 failed)

Expectation 1 (3 matchers):
    X HTTP method matches <PUT>
    X Path matches a string starting with "/alpha/bar"
    X Protocol matches equalToIgnoringCase("HTTPS")
    (3 matchers: 0 matched, 3 failed)
```

It will show the incoming request that was not matched with all of its known details, as well as a detailed explanation of the configured expectations
and each matcher it provides. Successful matches are marked with a checkmark (`✓`), and mis-matches with an `X`.

Alternately, you may specify the `reportToConsole true` configuration in the server config. This will cause the report to be written to the standard
output console as well as into the log output. This is useful for cases when you might have logging turned off.

==== Hamcrest Matchers

Many of the expectation methods accept http://hamcrest.org/[Hamcrest] `Matcher` instances as an alternate argument. Hamcrest matchers allow for a more
rich and expressive matching configuration. Consider the following configuration:

[source,groovy]
----
server.expectations {
    GET( startsWith('/foo') ){
        called greaterThanOrEqualTo(2)
        query 'user-key', notNullValue()
        responder {
            body 'ok', TEXT_PLAIN
        }
    }
}
----

This configuration would match a `GET` request to a URL starting with `/foo`, with a non-null query string "user-key" value. This request matcher is
 expected to be called at least twice and it will respond with a `text/plain` response of `ok`.

The methods that accept matchers will have a non-matcher version which provides a sensible default matcher (e.g. `GET(Matcher)` has `GET(String)` which
provides delegates to `GET( equalTo( string ) )` to wrap the provided path string in a matcher.

If you are using Groovy, you can actually replace Hamcrest matchers with a `Closure` emulating the same interface - basically a method that takes
the parameter and returns whether or not the condition was matched. The same example above could be re-written as:

[source,groovy]
----
server.expectations {
    GET({ p-> p.startsWith('/foo') }){
        called { i-> i >= 2 }
        query 'user-key', notNullValue()
        responder {
            body 'ok', TEXT_PLAIN
        }
    }
}
----

This allows for additional flexibility in configuring expectations.

==== Matching Cookies

There are four methods for matching cookies associated with a request (found in the `com.stehno.ersatz.cfg.Request` interface):

===== By Name and Matcher

The `cookie(String name, Matcher<Cookie> matcher)` method configures the specified matcher for the cookie with the given name.

[source,groovy]
----
server.expectations {
    GET('/somewhere'){
        cookie 'user-key', CookieMatcher.cookieMatcher {
            value startsWith('key-')
            domain 'mydomain.com'
        }
        responds().code(200)
    }
}
----

The Hamcrest matcher used may be a custom `Matcher` implementation, or the provided `com.stehno.ersatz.match.CookieMatcher`.

===== By Name and Value

The `cookie(String name, String value)` method is a shortcut for configuring simple name/value matching where the cookie value must be equal to the
specified value. An example:

[source,groovy]
----
server.expectations {
    GET('/somewhere').cookie('user-key', 'key-23435HJKSDGF86').responds().code(200)
}
----

This is equivalent to calling the matcher-based version of the method:

[source,groovy]
----
server.expectations {
    GET('/somewhere'){
        cookie 'user-key', CookieMatcher.cookieMatcher {
            value equalTo('key-23435HJKSDGF86')
        }
        responds().code(200)
    }
}
----

===== Multiple Cookies

The `cookies(Map<String,Object>)` method provides a means of configuring multiple cookie matchers (as value `String`s or cookie `Matcher`s). In the
following example matchers are configured to match the 'user-key' cookie for values "starting with" the specified value, the request should also have
an 'app-id' cookie with a value of "user-manager", and finally the request should _not_ have the 'timeout' cookie specified.

[source,groovy]
----
server.expectations {
    GET('/something'){
        cookies([
            'user-key': cookieMatcher {
                value startsWith('key-')
            },
            'appid': 'user-manager',
            'timeout': nullValue()
        ])
        responds().code(200)
    }
}
----

==== Overall Matcher

The `cookies(Matcher<Map<String,Cookie>)` method is used to specify a `Matcher` for the map of cookie names to `com.stehno.ersatz.cfg.Cookie` objects. The
matcher may be any custom matcher, or the `com.stehno.ersatz.match.NoCookiesMatcher` may be used to match for the case where no cookies should be defined
in the request:

[source,groovy]
----
server.expectations {
    get('/something'){
        cookies NoCookiesMatcher.noCookies()
        responds().code(200)
    }
}
----

=== Request / Response Compression

Ersatz supports GZip and Deflate compression seamlessly as long as the `Accept-Encoding` header is specified as `gzip` or `deflate`. If the response
is compressed, a `Content-Encoding` header will be added to the response with the appropriate compression type as the value.

=== Chunked Response

A response may be configured as a "chunked" response, wherein the response data is sent to the client in small bits along with an additional response
header, the `Transfer-encoding: chunked` header. For testing purposes, a fixed or randomized range of time delay may be configured so that the chunks
may be sent slowly, to more accurately simulate a real environment.

To configure a chunked response, provide a `ChunkingConfig` to the response configuration:

[source,groovy]
----
ersatzServer.expectations {
    GET('/chunky').responder {
        body 'This is chunked content', TEXT_PLAIN
        chunked {
            chunks 3
            delay 100..500
        }
    }
}
----

In the example, the response content will be broken into `3` roughly equal chunks, each of which is sent to the client after a random delay between 100 and
500 milliseconds. This `delay` value may also be a fixed number of milliseconds, or omitted to send the content as fast as possible.

TIP: The `Transfer-encoding` response header will be set automatically when a `chunked` configuration is specified on the response.

=== Multipart Request Content

Ersatz server supports multipart file upload requests (`multipart/form-data` content-type) using the
https://commons.apache.org/proper/commons-fileupload/[Apache File Upload] library on the "server" side. The expectations for multipart requests are
configured using the `MultipartRequestContent` class to build up an equivalent multipart matcher:

[source,groovy]
----
ersatz.expectataions {
    POST('/upload') {
        decoder MULTIPART_MIXED, Decoders.multipart
        decoder IMAGE_PNG, Decoders.passthrough
        body multipart {
            part 'something', 'interesting'
            part 'infoFile', 'info.txt', TEXT_PLAIN, infoText
            part 'imageFile', 'image.png', IMAGE_PNG, imageBytes
        }, MULTIPART_MIXED
        responder {
            body 'ok'
        }
    }
}
----

which will need to exactly match the incoming request body in order to be considered a match. There is also a `MultipartRequestMatcher` used to
provide a more flexible Hamcrest-based matching of the request body:

[source,groovy]
----
server.expectations {
    POST('/upload') {
        decoder MULTIPART_MIXED, Decoders.multipart
        decoder IMAGE_PNG, Decoders.passthrough
        body multipartMatcher {
            part 'something', notNullValue()
            part 'infoFile', endsWith('.txt'), TEXT_PLAIN, notNullValue()
            part 'imageFile', endsWith('.png'), IMAGE_PNG, notNullValue()
        }, MULTIPART_MIXED
        responder {
            body 'ok'
        }
    }
}
----

This will configure a match of the request body content based on the individual matchers, rather than overall equivalence.

A key point in multipart request support are the "decoders", which are used to decode the incoming request content into an expected object type.
Decoders are simply `BiFunction<byte[], DecodingContext, Object>` implementations - taking the incoming byte array, and a `DecodingContext` and
returning the decoded `Object` instance. Decoders may be registered in a shared instance of `RequestDecoders`, configured globally across the server
instance or configured on a per-request basis.

TIP: No decoders are provided by default, any used in the request content _must_ be provided in configuration.

Some common reusable decoders are provided in the `Decoders` utility class.

=== Multipart Response Content

Multipart response content is supported, though most browsers do not fully support it - the expected use case would be a RESTful or other HTTP-based
API. The response content will have the standard `multipart/form-data` content type and format. The response content parts are provided using an
instance of the `MultipartResponseContent` class along with the `Encoders.multipart` multipart response content encoder (configured on the server or
response).

The content parts are provided as "field" parts with only a field name and value, or as "file" parts with a field name, content-type, file name and
content object. These configurations are made on the `MultipartResponseContent` object via DSL or functional interface.

The part content objects are serialized for data transfer as `byte[]` content using configured encoders, which are simply instances of
`Function<Object,byte[]>` used to do the object to byte array conversion. These are configured either on a per-response basis or by sharing a
`ResponseEncoders` instance between multipart configurations - the shared encoders will be used if not explicitly overridden by the multipart
response configuration. No part encoders are provided by default.

An example multipart response with a field and an image file would be something like:

[source,groovy]
----
ersatz.expectations {
    GET('/data') {
        responder {
            encoder ContentType.MULTIPART_MIXED, MultipartResponseContent, Encoders.multipart
            body(multipart {
                // configure the part encoders
                encoder TEXT_PLAIN, CharSequence, { o -> (o as String).bytes }
                encoder IMAGE_JPG, File, { o -> ((File)o).bytes }

                // a field part
                field 'comments', 'This is a cool image.'

                // a file part
                part 'image', 'test-image.jpg', IMAGE_JPG, new File('/test-image.jpg'), 'base64'
            })
        }
    }
}
----

The resulting response body would look like the following (as a String):

----
--WyAJDTEVlYgGjdI13o
Content-Disposition: form-data; name="comments"
Content-Type: text/plain

This is a cool image.
--WyAJDTEVlYgGjdI13o
Content-Disposition: form-data; name="image"; filename="test-image.jpg"
Content-Transfer-Encoding: base64
Content-Type: image/jpeg

... more content follows ...
----

which could be decoded in the same manner a multipart _request_ content (an example using the Apache File Upload multipart parser can be found in
the unit tests).

== Proxy Server

Starting in v1.6.1 a standalone proxy server was available. The `com.stehno.ersatz.ErsatzProxy` is useful for testing proxied HTTP connections.
The proxy server has a similar configuration to the `ErsatzServer` and allows limited expectation configuration; it is expected that more detailed
expectation configuration will be done on a standard `ErsatzServer` instance at the other end of the proxy.

[source,groovy]
----
ErsatzServer ersatzServer = new ErsatzServer({
    expectations {
        GET('/').called(1).responds().code(200).body('Hello', TEXT_PLAIN)
        GET('/foo').called(1).responds().code(200).body('Foo!', TEXT_PLAIN)
    }
})

ErsatzProxy ersatzProxy = new ErsatzProxy({
    target ersatzServer.httpUrl
    expectations {
        GET('/')
        GET('/foo')
    }
})

String text = "${ersatzProxy.url}".toURL().text
assert text == 'Hello'

text = "${ersatzProxy.url}/foo".toURL().text
assert text == 'Foo!'

assert ersatzServer.verify()
assert ersatzProxy.verify()

ersatzProxy.stop()
ersatzServer.stop()
----

Notice that the `ErsatzProxy` has the same lifecycle as the `ErsatzServer`: you should call the `verify()` method to ensure that the expected requests
were proxied, and the `stop()` method must be closed once the server is no longer needed.

The requests made to the proxy server are passed through to the `targetUri` and the response from that server is returned as the response from the
proxy server.

The proxy server is usable from both Java and Groovy based on the use of Groovy `Closure`s or Java `Consumer`s for configuration.

Currently, the proxy server only supports HTTP endpoints (HTTPS will be supported in a future release once a bug is fixed in the underlying server).

== Sightings

Here are some external references and discussions about developing with Ersatz:

* http://guides.grails.org/grails-mock-http-server/guide/index.html[Consume and test a third-party REST API] _(Sergio del Amo)_ - Use Ersatz, a "mock" HTTP library, for testing code dealing with HTTP requests
* http://coffeaelectronica.com/blog/2017/rest-httpbuilder-ersatz.html[Taking a REST with HttpBulder-NG and Ersatz] _(Christopher J Stehno)_ - Building and testing a REST interface with HttpBuilder-NG and Ersatz (https://github.com/cjstehno/rest-dev[implementations] in Groovy, Java and Kotlin).
* The https://http-builder-ng.github.io/http-builder-ng/[HttpBuilder-NG] project has extensive examples of testing with Ersatz.

== Shadow Jar

The embedded version of Undertow used by Ersatz has caused issues with some server frameworks which also use Undertow (e.g. Grails, and Spring-boot).
If you run into errors using the standard jar distribution, please try using the `safe` distribution, which is a shadowed jar which includes the
Undertow library and its JBoss dependencies repackaged in the jar. You can use this version with the following coordinates:

    testCompile 'com.stehno.ersatz:ersatz:2.0.0:safe@jar'

For a Maven `pom.xml` entry, this would be:

    <dependency>
        <groupId>com.stehno.ersatz</groupId>
        <artifactId>ersatz</artifactId>
        <version>2.0.0</version>
        <type>jar</type>
        <scope>test</scope>
        <classifier>safe</classifier>
    </dependency>

Notice the `safe` classifier in both examples.

NOTE: This version of the library will NOT bring any of its other dependencies with it, so you will need to ensure that you have Hamcrest and JUnit defined, if they are needed by your project.

== Usage Examples

This section contains some recipe-style usage examples.

=== Url-Encoded Form Requests

Url-encoded form requests are supported by default when the request content-type is specified as `application/x-www-form-urlencoded`. The request
`body` expectation configuration will expect a `Map<String,String>` equivalent to the name-value pairs specified in the request body content. An
example would be:

[source,groovy]
----
server.expectations {
    POST('/form') {
        body([alpha: 'some data', bravo: '42'], 'application/x-www-form-urlencoded')
        responder {
            body 'ok'
        }
    }
}
----

where the `POST` content data would look like:

    alpha=some+data&bravo=42

=== File Upload (POST)

You can setup an expectation for a file upload POST using the `multipart` support, something like:

[source,groovy]
----
import com.stehno.erstaz.ErsatzServer
import com.stehno.ersatz.MultipartRequestContent
import static com.stehno.ersatz.ContentType.TEXT_PLAIN

def ersatz = new ErsatzServer({
    encoder TEXT_PLAIN, File, Encoders.text
})

def file = new File(/* some file */)

ersatz.expectations {
    POST('/upload') {
        decoders TEXT_PLAIN, Decoders.utf8String
        decoder MULTIPART_MIXED, Decoders.multipart

        body MultipartRequestContent.multipart {
            part 'fileName', file.name
            part 'file', file.name, 'text/plain; charset=utf-8', file.text
        }, MULTIPART_MIXED

        responder {
            body 'ok'
        }
    }
}
----

This will expect the posting of the given file content to the `/upload` path of the server.

=== File Download (GET)

Setting up an expectation for a GET request to respond with a file to download can be done as follows:

[source,groovy]
----
import com.stehno.erstaz.ErsatzServer
import static com.stehno.ersatz.ContentType.TEXT_PLAIN

def ersatz = new ErsatzServer({
    encoder TEXT_PLAIN, File, Encoders.text
})

def file = new File(/* some file */)

ersatz.expectations {
    GET('/download'){
        responder {
            header 'Content-Disposition', "attachment; filename=\"${file.name}\""
            body file, TEXT_PLAIN
        }
    }
}
----

This will respond to the request with file download content.

=== Kotlin Usage

You can use the Ersatz Server from the Kotlin programming language just as easily as Java or Groovy:

[source,kotlin]
----
val ersatz = ErsatzServer { config -> config.autoStart(true) }

ersatz.expectations { expectations ->
    expectations.GET("/kotlin").called(1).responder { response ->
        response.body("Hello Kotlin!", ContentType.TEXT_PLAIN).code(200)
    }
}

val http = OkHttpClient.Builder().build()
val request: okhttp3.Request = okhttp3.Request.Builder().url("${ersatz.httpUrl}/kotlin").build()
println( http.newCall(request).execute().body().string() )
----

which will print out "Hello Kotlin!" when executed.

=== Matching XML Body Content

A unit test has been added to provide a better example of how to use the Hamcrest matchers in a request. See the `BodyContentMatcherSpec` test class in the source code, but
a summary is provided below:

[source,groovy]
----
import com.stehno.ersatz.encdec.DecodingContext
import com.stehno.ersatz.ErsatzServer
import com.stehno.ersatz.util.HttpClient
import okhttp3.MediaType
import okhttp3.Response
import spock.lang.AutoCleanup
import spock.lang.Specification

import javax.xml.parsers.DocumentBuilderFactory

import static com.stehno.ersatz.encdec.ContentType.TEXT_XML
import static com.stehno.ersatz.encdec.Decoders.utf8String
import static com.stehno.ersatz.encdec.Encoders.text
import static okhttp3.RequestBody.create
import static org.hamcrest.CoreMatchers.equalTo
import static org.hamcrest.xml.HasXPath.hasXPath

class BodyContentMatcherSpec extends Specification {

    @AutoCleanup private final ErsatzServer server = new ErsatzServer()
    private final HttpClient http = new HttpClient()

    void 'matching part of body content'() {
        setup:
        String requestXml = '<request><node foo="bar"/></request>'
        String responseXml = '<response>OK</response>'

        server.expectations {
            POST('/posting') {
                decoder('text/xml; charset=utf-8') { byte[] bytes, DecodingContext ctx ->
                    DocumentBuilderFactory.newInstance().newDocumentBuilder().parse(new ByteArrayInputStream(bytes))
                }
                body hasXPath('string(//request/node/@foo)', equalTo('bar')), 'text/xml; charset=utf-8'
                called 1
                responder {
                    body responseXml, TEXT_XML
                    encoder TEXT_XML, String, text
                }
            }
        }

        when:
        Response response = http.post(server.httpUrl('/posting'), create(MediaType.get('text/xml; charset=utf-8'), requestXml))

        then:
        response.body().string() == responseXml

        when:
        response = http.post(server.httpUrl('/posting'), create(MediaType.get('text/xml; charset=utf-8'), '<request><node foo="blah"/></request>'))

        then:
        response.code() == 404

        and:
        server.verify()
    }
}
----

This test sets up a POST expectation with the XML request body content being used as one of the matching criteria. Hamcrest provides an XPath-based
matcher, `hasXPath(String, Matcher)`, which works well here. A custom XML-decoder was installed to parse the request into the XML document format
required by the matcher.

The test shows to requests made to the server, one with the expected content and one without - the results verify that only the correct call was
actually matched.

See the http://hamcrest.org/JavaHamcrest/[Hamcrest] documentation for more details about pre-existing and custom `Matcher`s.