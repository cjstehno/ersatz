plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.gems' version '3.3.2'
    id 'com.stehno.gradle.webpreview' version '0.3.0'
}

group = 'io.github.cjstehno.ersatz'
version = '3.0.0'

repositories {
    mavenCentral()
}

asciidoctor {
    baseDir file('src/docs/asciidoc')
    options doctype: 'book'
    attributes(
        'source-highlighter': 'rouge',
        'coderay-linenums-mode': 'table',
        icon: 'font',
        linkattrs: true,
        encoding: 'utf-8'
    )
}

webPreview {
    resourceDir = file('build/site')
}

task site(dependsOn: ['build', 'ersatz:javadoc', 'ersatz:jacocoTestReport', 'ersatz-groovy:javadoc', 'ersatz-groovy:jacocoTestReport',  'asciidoctor']) {
    doLast {
        def vars = [
            project_version: project.version,
            year           : Calendar.instance.get(Calendar.YEAR)
        ]

        mkdir 'build/site'

        copy {
            from 'src/site'
            into 'build/site'
            include '**/*.html'
            expand vars
        }

        copy {
            from 'src/site'
            into 'build/site'
            include '**/css/**'
            include '**/js/**'
            include '**/img/**'
        }

        // Copy the ersatz coverage reports
        copy {
            from 'ersatz/build/reports/jacoco/test/html'
            include '**/**'
            into 'build/site/ersatz/jacoco'
        }

        // Copy the ersatz test reports
        copy {
            from 'ersatz/build/reports/tests/test'
            include '**/**'
            into 'build/site/ersatz/tests'
        }

        // Copy the ersatz javadocs
        copy {
            from 'ersatz/build/docs/javadoc'
            include '**/**'
            into 'build/site/ersatz/javadoc'
        }

        // Copy the ersatz-groovy coverage reports
        copy {
            from 'ersatz-groovy/build/reports/jacoco/test/html'
            include '**/**'
            into 'build/site/ersatz-groovy/jacoco'
        }

        // Copy the ersatz-groovy test reports
        copy {
            from 'ersatz-groovy/build/reports/tests/test'
            include '**/**'
            into 'build/site/ersatz-groovy/tests'
        }

        // Copy the ersatz-groovy javadocs
        copy {
            from 'ersatz-groovy/build/docs/javadoc'
            include '**/**'
            into 'build/site/ersatz-groovy/javadoc'
        }

        // copy the generated user-guide
        copy {
            from 'build/docs/asciidoc'
            include 'user_guide.html'
            into 'build/site/docs'
        }
    }
}

//task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
//    buildFile = 'publish.gradle'
//    tasks = ['gitPublishPush']
//}