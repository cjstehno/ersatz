plugins {
    id 'groovy'
    id "com.github.hierynomus.license" version '0.14.0'
    id 'jacoco'
    id 'codenarc'
    id 'maven'
    id 'maven-publish'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.jfrog.bintray' version '1.8.4'
    id "org.asciidoctor.convert" version '1.5.8.1'
    id "com.stehno.gradle.webpreview" version '0.3.0'
    id "com.stehno.gradle.site" version '0.0.3'
    id 'org.jetbrains.kotlin.jvm' version '1.2.71'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id "com.github.johnrengelman.shadow" version '4.0.1'
    id "com.adarshr.test-logger" version "1.5.0"
}

group = 'com.stehno.ersatz'
version = '1.9.0'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

configurations {
    groovyDoc
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"

    implementation 'io.undertow:undertow-core:2.0.13.Final'

    implementation 'ch.qos.logback:logback-classic:1.1.7'

    implementation 'commons-fileupload:commons-fileupload:1.3.3'
    implementation 'javax.servlet:javax.servlet-api:3.1.0'

    // closure handling
    implementation 'space.jasan:groovy-closure-support:0.4.0'

    // these are used to provide junit helpers in production code
    compile 'org.hamcrest:hamcrest-library:1.3'
    implementation 'junit:junit:4.12'
    implementation "org.junit.jupiter:junit-jupiter-api:5.3.1"

    testImplementation "org.junit.jupiter:junit-jupiter-params:5.3.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.3.1'

    testCompile 'com.squareup.okhttp3:okhttp:3.11.0'
    testCompile 'org.spockframework:spock-core:1.2-groovy-2.4'

    testCompile 'com.burgstaller:okhttp-digest:1.18'
    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.71"

    groovyDoc "org.codehaus.groovy:groovy-groovydoc:$groovyVersion"
    groovyDoc "org.codehaus.groovy:groovy-ant:$groovyVersion"

    codenarc(
        'org.codenarc:CodeNarc:1.2.1',
        "org.codehaus.groovy:groovy-all:$groovyVersion"
    )
}

codenarc {
    toolVersion = '1.2.1'
}

codenarcMain {
    ignoreFailures false
    configFile file('config/codenarc-main.rules')

    maxPriority1Violations 0
    maxPriority2Violations 5
    maxPriority3Violations 10
}

codenarcTest {
    ignoreFailures true
    configFile file('config/codenarc-test.rules')
}

groovydoc {
    groovyClasspath = configurations.groovyDoc
}

task sourcesJar(type: Jar) {
    description = 'Creates sources jar.'
    classifier = 'sources'
    from project.sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    description = 'Creates API documentation jar.'
    classifier = 'javadoc'
    from groovydoc
}

test {
    useJUnitPlatform()
    maxParallelForks = Math.min(Runtime.runtime.availableProcessors(), 8)
//    useJUnit {
//        if (project.hasProperty('skip-long-running')) {
//            excludeCategories 'com.stehno.ersatz.LongRunning'
//        }
//    }
}

testlogger {
  theme 'standard-parallel'
  showPassed false
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

publishing {
    publications {
        ersatzPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact shadowJar
            groupId project.group
            artifactId 'ersatz'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Mock server library for testing HTTP clients.')
                root.appendNode('name', 'ersatz')
                root.appendNode('url', 'http://stehno.com/ersatz')

                def licenseNode = root.appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'The Apache Software License, Version 2.0')
                licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                licenseNode.appendNode('distribution', 'repo')

                def developersNode = root.appendNode('developers').appendNode('developer')
                developersNode.appendNode('id', 'cjstehno')
                developersNode.appendNode('name', 'Christopher J Stehno')
                developersNode.appendNode('email', 'chris@stehno.com')

                def scmNode = root.appendNode('scm')
                scmNode.appendNode('connection', 'scm:git:git@github.com:cjstehno/ersatz.git')
                scmNode.appendNode('developerConnection', 'scm:git:git@github.com:cjstehno/ersatz.git')
                scmNode.appendNode('url', 'https://github.com/cjstehno/ersatz')
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

license {
    header = file('config/license_header.txt')

    ignoreFailures false

    ext.year = Calendar.instance.get(Calendar.YEAR)

    excludes(['**/*.txt', '**/*.dtd', '**/*.xml', '**/*.ent', '**/*.jks', '**/*.jpg'])
}

bintray {
    if (project.hasProperty('user') && project.hasProperty('key')) {
        user = project.property('user')
        key = project.property('key')

        publications = ['ersatzPublication']

        pkg {
            repo = 'stehno'
            name = 'ersatz'
            desc = 'Mock server library for testing HTTP clients.'
            licenses = ['Apache-2.0']
            labels = ['groovy']
            websiteUrl = 'http://stehno.com/ersatz'
            issueTrackerUrl = 'https://github.com/cjstehno/ersatz/issues'
            vcsUrl = 'https://github.com/cjstehno/ersatz'
            publicDownloadNumbers = true

            version {
                name = project.version
                vcsTag = "v${project.version}"
                gpg {
                    sign = true
                }
                mavenCentralSync {
                    sync = true
                    user = project.property('sonotypeUser')
                    password = project.property('sonotypePass')
                }
            }
        }
    }
}

asciidoctor {
    options doctype: 'book'

    backends = ['html5']

    attributes 'source-highlighter': 'coderay',
        'coderay-linenums-mode': 'table',
        icon: 'font',
        linkattrs: true,
        encoding: 'utf-8'
}

site {
    versionedFile 'src/docs/asciidoc/index.adoc'
}

tasks.site.dependsOn = ['build', 'groovydoc', 'jacocoTestReport', 'asciidoctor']

task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
    buildFile = 'publish.gradle'
    tasks = ['gitPublishPush']
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

shadowJar {
    classifier = 'safe'
    dependencies {
        exclude(dependency('junit:junit:4.12'))
        exclude(dependency("org.codehaus.groovy:groovy-all:$groovyVersion"))
        exclude(dependency('org.slf4j:slf4j-api:1.7.20'))
        exclude(dependency('ch.qos.logback:logback-classic:1.1.7'))
        exclude(dependency('ch.qos.logback:logback-core:1.1.7'))
        exclude(dependency('org.hamcrest:hamcrest-library:1.3'))
        exclude(dependency('org.hamcrest:hamcrest-core:1.3'))
        exclude(dependency('org.junit.jupiter:junit-jupiter-params:5.3.1'))
        exclude(dependency('org.junit.jupiter:junit-jupiter-engine:5.3.1'))
        exclude(dependency('org.junit.vintage:junit-vintage-engine:5.3.1'))
        exclude(dependency('org.junit.jupiter:junit-jupiter-api:5.3.1'))
        exclude(dependency('org.junit.platform:junit-platform-commons:1.3.1'))
        exclude(dependency('org.junit.platform:junit-platform-engine:1.3.1'))
        exclude(dependency('org.opentest4j:opentest4j:1.1.1'))
        exclude(dependency('org.apiguardian:apiguardian-api:1.0.0'))
    }

    mergeServiceFiles()

    relocate 'io.undertow', 'ersatz.undertow'
    relocate 'org.jboss', 'ersatz.jboss'
    relocate 'org.xnio', 'ersatz.xnio'
    relocate 'javax.servlet', 'ersatz.servlet'
    relocate 'org.apache.commons.fileupload', 'ersatz.fileupload'
    relocate 'org.apache.commons.io', 'ersatz.io'
}
