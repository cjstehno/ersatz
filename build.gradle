plugins {
    id 'groovy'
    id "com.github.hierynomus.license" version "0.13.1"
    id 'jacoco'
    id 'codenarc'
    id 'maven'
    id 'maven-publish'
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'com.jfrog.bintray' version '1.7.3'
    id "org.asciidoctor.convert" version "1.5.3"
    id "com.stehno.gradle.webpreview" version "0.1.1"
}

group = 'com.stehno.ersatz'
version = '0.4.3'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

configurations {
    groovyDoc
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.8'
    compile 'io.undertow:undertow-core:1.4.6.Final'

    compile 'ch.qos.logback:logback-classic:1.1.7'

    compile 'commons-fileupload:commons-fileupload:1.3.2'
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    compile 'org.hamcrest:hamcrest-library:1.3'

    testCompile 'com.squareup.okhttp3:okhttp:3.5.0'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:3.2.4'

    groovyDoc 'org.codehaus.groovy:groovy-groovydoc:2.4.8'
    groovyDoc 'org.codehaus.groovy:groovy-ant:2.4.8'

    codenarc(
        'org.codenarc:CodeNarc:0.26.0',
        'org.codehaus.groovy:groovy-all:2.4.8'
    )
}

codenarc {
    toolVersion = '0.26.0'
}

codenarcMain {
    ignoreFailures false
    configFile file('config/codenarc-main.rules')

    maxPriority1Violations 0
    maxPriority2Violations 5
    maxPriority3Violations 10
}

codenarcTest {
    ignoreFailures true
    configFile file('config/codenarc-test.rules')
}

groovydoc {
    groovyClasspath = configurations.groovyDoc
}

task sourcesJar(type: Jar) {
    description = 'Creates sources JAR.'
    classifier = 'sources'
    from project.sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

project.publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            version project.version
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

license {
    header rootProject.file('config/license_header.txt')

    // Toggle ignoreFailures to false to enable license-checking that will fail the build
    ignoreFailures true

    ext.name = 'Christopher J. Stehno'
    ext.year = Calendar.instance.get(Calendar.YEAR)

    excludes(['**/*.txt', '**/*.dtd', '**/*.xml', '**/*.ent', '**/*.jks'])
}

bintray {
    if (project.hasProperty('user') && project.hasProperty('key')) {
        user = project.property('user')
        key = project.property('key')

        publications = ['mavenJava']

        pkg {
            repo = 'stehno'
            name = 'ersatz'
            desc = 'Mock server library for testing HTTP clients.'
            licenses = ['Apache-2.0']
            labels = ['groovy']
            vcsUrl = 'https://github.com/cjstehno/ersatz'
        }
    }
}

asciidoctor {
    options doctype: 'book'

    backends = ['html5']

    attributes 'source-highlighter': 'coderay',
        'coderay-linenums-mode': 'table',
        icon: 'font',
        linkattrs: true,
        encoding: 'utf-8'
}

task site(group: 'Documentation', description: 'Builds the documentation web site.', dependsOn: ['build', 'groovydoc', 'jacocoTestReport', 'asciidoctor']) {
    doLast {
        logger.info 'Building documentation web site...'

        // copy the templates
        copy {
            from 'src/site'
            into 'build/site'
            include '**/*.html'
            expand([project_version: project.version, year: Calendar.instance.get(Calendar.YEAR)])
        }

        // copy the assets
        copy {
            from 'src/site'
            into 'build/site'
            include '**/stylesheets/**'
            include '**/images/**'
        }

        copy {
            from 'build/docs'
            from 'build/reports'
            include '**/**'
            into 'build/site'
        }

        copy {
            from 'build/asciidoc'
            include '**/**'
            into 'build/site/guide'
        }
    }
}

task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
    buildFile = 'publish.gradle'
    tasks = ['publishGhPages']
}

task updateVersion(group: 'Documentation', description: 'Updates the version in documentation based on the project version') {
    doLast {
        String newVersion = project.version
        String oldVersion = project.property('from')

        logger.lifecycle "Updating documentation versions from ${oldVersion} to ${newVersion}..."

        ant.replace(file: 'README.md', token: oldVersion, value: newVersion)
        ant.replace(dir: 'src/docs/asciidoc', token: oldVersion, value: newVersion)
    }
}

task checkVersion(group: 'Verification', description: 'Verify that the project version is reflected in the documentation.') {
    doLast {
        logger.lifecycle "Verifying that the documentation references version ${project.version}..."

        // Not the most efficient way to do this but should be ok for now
        boolean documented = ['README.md', 'src/docs/asciidoc/index.adoc'].every { f ->
            boolean ok = project.file(f).text.contains(project.version)

            logger.info " - Checking: $f: $ok"
            ok
        }

        assert documented, 'The documented project version does not match the project version: Run "./gradlew updateVersion -Pfrom=OLD_VERSION" and try again.'
    }
}

webPreview {
    resourceDir = file('build/site')
}